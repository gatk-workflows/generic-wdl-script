## Copyright Broad Institute, 2018
## Purpose: 
## This WDL workflowi runs a generic command
##
## Requirements/expectations :
## - A command to execute
## - An array of input files
## - An output directory used in the command, should not be current working directory
##   - Example: /outputs/
##
## Outputs :
## - An array of output files generated by command
##
## Cromwell version support 
## - Successfully tested on v36
## - Does not work on versions < v23 due to output syntax
##
## Runtime parameters are optimized for Broad's Google Cloud Platform implementation.
##
## LICENSING : 
## This script is released under the WDL source code license (BSD-3) (see LICENSE in 
## https://github.com/broadinstitute/wdl). Note however that the programs it calls may 
## be subject to different licenses. Users are responsible for checking that they are
## authorized to run all programs before running this script. Please see the dockers
## for detailed licensing information pertaining to the included programs.



# WORKFLOW DEFINITION 

workflow genericworkflow {

  String? docker_override
  String docker = select_first([docker_override, "ubuntu:14.04"])

  call GenericTask {
    input:
      docker = docker
  }

}


# TASK DEFINITIONS
task GenericTask {
    # Command parameters
    Array[File] input_files
    String output_dir
    String shell_command

    # Runtime parameters
    String docker
    Int? machine_mem_gb
    Int machine_mem = select_first([machine_mem_gb, 7])
    Int? disk_space_gb
    Boolean use_ssd = false
    Int? cpu
    Int? preemptible_attempts

    command <<<
        set -eo pipefail
        mv -t . ${sep=' ' input_files}
        mkdir ${output_dir}
        ${shell_command}
        find ${output_dir} -type f > listofoutputfiles.txt
    >>>

    runtime {
        docker: docker
        memory: machine_mem + " GB"
        disks: "local-disk " + select_first([disk_space_gb, 100]) + if use_ssd then " SSD" else " HDD"
        cpu: select_first([cpu, 1])
        preemptible: select_first([preemptible_attempts, 3])
    }

    output {
        Array[File] output_files = read_lines("listofoutputfiles.txt")
    }
}
